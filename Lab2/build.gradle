plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'name.remal.sonarlint' version '1.0.211'
    id 'org.sonarqube' version '3.0'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.2'
    id 'name.remal.check-updates' version '1.0.211'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group 'com.nelkinda.training.tdd-java'
version '0.0.1-SNAPSHOT'

// TODO Add properties to use Java 14

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependency 'com.puppycrawl.tools:checkstyle:8.36'
        dependencySet(group: 'net.sourceforge.pmd', version: '6.27.0') {
            entry 'pmd'
            entry 'pmd-core'
            entry 'pmd-java'
        }
        // TODO Add dependency set to select JUnit version
        // TODO Add dependency set to select JUnit platform version
    }
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle'

    // TODO Add dependency to use JUnit
    // TODO Add dependency to use JUnit platform
}

checkDependencyUpdates {
    failIfUpdatesFound = false // Workaround for sonarlint version issues.
}

checkstyleMain {
    configFile = file('config/checkstyle/checkstyleMain.xml')
    maxWarnings = 0
}

checkstyleTest {
    configFile = file('config/checkstyle/checkstyleTest.xml')
    maxWarnings = 0
}

pmd {
    ruleSets = []
    consoleOutput = true
    dependencies {
        pmd 'net.sourceforge.pmd:pmd'
        pmd 'net.sourceforge.pmd:pmd-core'
        pmd 'net.sourceforge.pmd:pmd-java'
    }
}

pmdMain {
    ruleSetFiles = files('config/pmd/pmdMain.xml')
}

pmdTest {
    ruleSetFiles = files('config/pmd/pmdTest.xml')
}

test {
    // TODO Select the JUnit 5 platform
}

wrapper {
    gradleVersion '6.6.1'
    distributionType = Wrapper.DistributionType.ALL
}
